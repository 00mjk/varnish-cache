varnishtest "Check saint mode with sick pages"

feature OldSaintMode

server s1 {
	timeout 10

	rxreq
	expect req.url == "/"
	txresp -status 200 -hdr "foo: 1"
	rxreq
	expect req.url == "/"
	txresp -status 200 -hdr "foo: 2"
	accept
	rxreq
	expect req.url == "/"
	txresp -status 200 -hdr "foo: 3"
} -start

varnish v1 -vcl+backend {
	sub vcl_backend_response {
		set beresp.ttl = 1s;
		set beresp.grace = 10m;
		if (beresp.http.foo == "2")
		{
			set beresp.saintmode = 2s;
			set beresp.status = 606;
			set beresp.uncacheable = true;
			set beresp.ttl = 0s;
			return (deliver);
		}
		return(deliver);
	}
	sub vcl_deliver {
		if (resp.status == 606) {
			return (restart);
		}
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1
} -run

delay 2

client c2 {
	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 1
} -run

delay 2

client c3 {
	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.foo == 3
} -run
