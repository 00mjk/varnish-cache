varnishtest "test caching of req.body"

server s1 {
	rxreq
	expect req.bodylen == 3
	txresp -hdr "Connection: close" -hdr "Foo: BAR" -body "1234"

	expect_close
	accept

	rxreq
	expect req.bodylen == 3
	txresp -hdr "Foo: Foo" -body "56"
} -start

varnish v1 -vcl+backend {
	import std;

	sub vcl_recv {
		if (req.url != "/wrong-sub") {
			set req.http.stored = std.cache_req_body(1KB);
		}
		return (pass);
	}

	sub vcl_deliver {
		if (resp.http.foo == "BAR") {
			return (restart);
		}
		set resp.http.stored = req.http.stored;
	}

	sub vcl_backend_fetch {
		if (bereq.url == "/wrong-sub") {
			if (std.cache_req_body(1KB)) {
				return (error(200));
			} else {
				return (error(503));
			}
		}
	}
} -start

varnish v1 -cliok "param.set debug +syncvsl"

client c1 {
	txreq -req "POST" -body "FOO"
	rxresp
	expect resp.http.Foo == "Foo"
	expect resp.bodylen == 2
	expect resp.http.stored == true
} -run

# check log for the aborted POST
logexpect l1 -v v1 {
	expect * 1006	Begin
	expect * =	FetchError	"^straight insufficient bytes"
} -start

client c2 {
	txreq -req POST -hdr "Content-Length: 52"
} -run

logexpect l1 -wait

# wrong calling context
client c3 {
	txreq -url "/wrong-sub"
	rxresp
	expect resp.status == 503
} -run

delay .1

varnish v1 -expect MGT.child_died == 0

# no req body
server s1 {
	rxreq
	txresp
} -start

client c4 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.stored == true
} -run

# req body overflow
client c5 {
	txreq -req POST -hdr "Content-Length: 1025"
	expect_close
} -run

varnish v1 -errvcl {req.body can only be cached in vcl_recv} {
	import std;
	backend none none;
	sub vcl_init {
		if (! std.cache_req_body(1KB)) {
			return (fail);
		}
	}
}
