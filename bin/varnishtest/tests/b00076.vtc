varnishtest "hash_data() in vcl_recv{}"

server s1 {
	rxreq
	txresp -hdr "Same: One"
} -start

varnish v1 -vcl+backend {
    sub vcl_recv {
	if (req.url == "/2") {
		hash_data(req.http.foo);
	}
    }
    sub vcl_hash {
	hash_data(req.url);
	return (lookup);
    }
} -start

varnish v1 -cliok "param.set vsl_mask +Hash"

client c1 {
	txreq -url /1
	rxresp
	expect resp.status == 200
	expect resp.http.same == One
	txreq -url /2 -hdr "foo: /1"
	rxresp
	expect resp.status == 200
	expect resp.http.same == One
} -run

server s1 {
	rxreq
	txresp -hdr "Second: One"
} -start

varnish v1 -vcl+backend {
    sub make_hash_key {
        hash_data("Documented Design Pattern");
        hash_data(req.url);
    }
    
    sub vcl_hash {
        call make_hash_key;
        return (lookup);
    }
 
    sub vcl_recv {
        if (req.http.early) {
            call make_hash_key;
        }
    }
}

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.second == One

	txreq -hdr "early: yes"
	rxresp
	expect resp.status == 200
	expect resp.http.second == One
} -run

