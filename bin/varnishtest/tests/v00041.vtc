varnishtest "Test priv_sess and priv_req"

server s1 {
	rxreq
	txresp
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_debug};

	sub vcl_recv {
		set req.http.x0 = debug.test_priv_req(req.url);
		set req.http.y0 = debug.test_priv_sess(req.url);
	}

	sub vcl_deliver {
		set resp.http.x0 = req.http.x0;
		set resp.http.x1 = debug.test_priv_req("");
		set resp.http.y0 = req.http.y0;
		set resp.http.y1 = debug.test_priv_sess("");
	}
} -start


client c1 {
	txreq -url /foobar
	rxresp
	expect resp.http.x0 == /foobar
	expect resp.http.x1 == /foobar
	expect resp.http.y0 == /foobar
	expect resp.http.y1 == /foobar

	txreq -url /snafu
	rxresp
	expect resp.http.x0 == /snafu
	expect resp.http.x1 == /snafu
	expect resp.http.y0 == /foobar
	expect resp.http.y1 == /foobar
} -run
